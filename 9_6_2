		LIST	P=PIC16F84A
		#INCLUDE <P16F84A.INC>

		__CONFIG _HS_OSC & _CP_OFF & _WDT_OFF & _PWRTE_ON

W_TEMP		EQU	0CH
STATUS_TEMP	EQU	0DH
CNT1		EQU	0EH
CNTB		EQU	0FH
TMPA		EQU 010H
OUT			EQU	011H
IN			EQU	012H
NUM11		EQU	013H
NUM12		EQU	014H
CHAR1		EQU	015H	;+.-
NUM21		EQU 016H
NUM22		EQU	017H	;NUM11NUM12 + NUM21NUM22
CHAR2		EQU	018H	;=
CNTADL		EQU	019H	;間接アドレッシング用カウンタ
ANS1		EQU	020H
ANS2		EQU 021H
ANS3		EQU	023H

			ORG	0H
			GOTO	MAIN

			ORG	04H
PUSH
			MOVWF	W_TEMP
			MOVF	STATUS,W
			MOVWF	STATUS_TEMP
BODY
			MOVLW	013H
			ADDWF	CNTADL,W
			MOVWF	FSR
			;間接アドレッシングでデータ入力
			CALL	CRECV
			MOVWF	INDF
			INCF	CNTADL,F
			MOVF	CNTADL,W
			CALL	CSEND
	
			;CNTADLが6ならCHECK->CALCへ
			MOVF	CNTADL,W	
			XORLW	06H
			BTFSC	STATUS,Z
			CALL	CHECK
			
POP
			MOVF	STATUS_TEMP,W
			MOVWF	STATUS
			SWAPF	W_TEMP,F
			SWAPF	W_TEMP,W
			BCF		INTCON,INTF
			RETFIE

MAIN
			BSF		STATUS,RP0
			BCF		INTCON,GIE
			CLRF	TRISA
			MOVLW	0FH
			MOVWF	TRISB
			BCF		OPTION_REG,INTEDG
			BCF		STATUS,RP0
			CLRF	CNTADL
			CLRF	NUM11
			CLRF	NUM12
			CLRF	NUM21
			CLRF	NUM22
			BSF		INTCON,GIE
			BSF		INTCON,INTE

MAINLP
			GOTO	MAINLP


CRECV	;PC->Wreg
			CLRF	TMPA
			MOVLW	08H
			MOVWF	CNTB
			CALL	TIME156
CRECVLP
			BTFSC	PORTB,0
			BSF		STATUS,C
			BTFSS	PORTB,0
			BCF		STATUS,C
			RRF		TMPA,F
			CALL	TIME104
			DECFSZ	CNTB,F
			GOTO	CRECVLP
			MOVF	TMPA,W
			RETURN

CSEND	;Wreg->PC
			MOVWF	IN
			MOVLW	08H
			MOVWF	CNTB
			BCF		PORTA,4
			CALL	TIME104

CSENDLP
			BTFSC	IN,0
			BSF		PORTA,4
			BTFSS	IN,0
			BCF		PORTA,4
			CALL	TIME104
			RRF		IN,F
			DECFSZ	CNTB,F
			GOTO	CSENDLP
			BSF		PORTA,4
			CALL	TIME104
			RETURN


TIME156
			MOVLW	0C3H
			MOVWF	CNT1
LOOP156
			NOP
			DECFSZ	CNT1,F
			GOTO	LOOP156
			RETURN

TIME104
			MOVLW	082H
			MOVWF	CNT1
LOOP
			NOP
			DECFSZ	CNT1,F
			GOTO	LOOP
			RETURN
CHECK
			;ヘンな入力じゃないか検証
			MOVLW	'0'
			SUBWF	NUM11,W
			BTFSS	STATUS,C
			GOTO	ERR
			MOVF	NUM11,W
			SUBLW	'9'
			BTFSS	STATUS,C
			GOTO	ERR

			MOVLW	'0'
			SUBWF	NUM12,W
			BTFSS	STATUS,C
			GOTO	ERR
			MOVF	NUM12,W
			SUBLW	'9'
			BTFSS	STATUS,C
			GOTO	ERR

			MOVLW	'0'
			SUBWF	NUM21,W
			BTFSS	STATUS,C
			GOTO	ERR
			MOVF	NUM21,W
			SUBLW	'9'
			BTFSS	STATUS,C
			GOTO	ERR

			MOVLW	'0'
			SUBWF	NUM22,W
			BTFSS	STATUS,C
			GOTO	ERR
			MOVF	NUM22,W
			SUBLW	'9'
			BTFSS	STATUS,C
			GOTO	ERR

			MOVF	CHAR1,W
			XORLW	'+'
			BTFSS	STATUS,Z
			GOTO	ERR

			MOVF	CHAR2,W
			XORLW	'='
			BTFSS	STATUS,Z
			GOTO	ERR	
;CALC
			;計算、送信->そのままURESETへ
			CLRF	ANS1
			CLRF	ANS2
			CLRF	ANS3
			MOVLW	'0'
			SUBWF	NUM11,F
			SUBWF	NUM12,F
			SUBWF	NUM21,F
			SUBWF	NUM22,F

			;1の位
			MOVF	NUM12,W
			ADDWF	NUM22,W
			MOVWF	ANS1
			;繰り上がり判定
			MOVLW	0AH
			SUBWF	ANS1,W
			BTFSC	STATUS,C
			CALL	MOVEUP1

			;10の位
			MOVF	NUM11,W
			ADDWF	NUM21,W
			ADDWF	ANS2
			;繰り上がり判定
			MOVLW	0AH
			SUBWF	ANS2,W
			BTFSC	STATUS,C
			CALL	MOVEUP2
			;送信
			GOTO	SEND

MOVEUP1
			MOVWF	ANS1
			INCF	ANS2
			RETURN
MOVEUP2
			MOVWF	ANS2
			INCF	ANS3
			RETURN

SEND
			;ANS3=0ならスキップ
			BTFSS	ANS3,0
			GOTO	SKPANS3
			MOVF	ANS3,W
			ADDLW	'0'
			CALL	CSEND
SKPANS3
			MOVF	ANS2,W
			ADDLW	'0'
			CALL	CSEND
			MOVF	ANS1,W
			ADDLW	'0'
			CALL	CSEND
			GOTO	URESET

ERR			
			MOVLW	'E'
			CALL	CSEND
			MOVLW	'R'
			CALL	CSEND
			MOVLW	'R'
			CALL	CSEND
			MOVLW	'O'
			CALL	CSEND
			MOVLW	'R'
			CALL	CSEND
URESET
			CLRF	CNTADL
			MOVLW	'\n'
			CALL	CSEND
			MOVLW	'\r'
			CALL	CSEND
			RETURN			

			END